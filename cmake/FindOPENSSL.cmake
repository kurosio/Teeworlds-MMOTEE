if(NOT CMAKE_CROSSCOMPILING)
  find_package(PkgConfig QUIET)
  pkg_check_modules(PC_CRYPTO libcrypto)
endif()

# TODO: idk why but encounter dependencies so we first check the PkgConfig path
set_extra_dirs_lib(OPENSSL openssl)
find_library(OPENSSL_CRYPTO_LIBRARY
  NAMES "libcrypto"
  HINTS ${PC_CRYPTO_LIBDIR} ${PC_CRYPTO_LIBRARY_DIRS} ${HINTS_OPENSSL_LIBDIR}
  PATHS ${PATHS_OPENSSL_LIBDIR}
  ${CROSSCOMPILING_NO_CMAKE_SYSTEM_PATH})

set_extra_dirs_include(OPENSSL openssl "${OPENSSL_CRYPTO_LIBRARY}")
find_path(OPENSSL_INCLUDEDIR 
  NAMES "openssl/crypto.h"
  HINTS ${PC_CRYPTO_INCLUDEDIR} ${PC_CRYPTO_INCLUDE_DIR} ${HINTS_OPENSSL_INCLUDEDIR} 
  PATHS ${PATHS_OPENSSL_INCLUDEDIR}
  ${CROSSCOMPILING_NO_CMAKE_SYSTEM_PATH})

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(OPENSSL DEFAULT_MSG OPENSSL_CRYPTO_LIBRARY OPENSSL_INCLUDEDIR)
mark_as_advanced(OPENSSL_CRYPTO_LIBRARY OPENSSL_INCLUDEDIR)

set(OPENSSL_CRYPTO_LIBRARIES)
set(OPENSSL_INCLUDE_DIRS)

if(OPENSSL_FOUND)
  set(OPENSSL_CRYPTO_LIBRARIES ${OPENSSL_CRYPTO_LIBRARY})
  set(OPENSSL_INCLUDE_DIRS ${OPENSSL_INCLUDEDIR})
  set(OPENSSL_COPY_FILES)
  
  if(TARGET_OS AND TARGET_OS STREQUAL "windows")
    if(TARGET_BITS EQUAL 32)
      set(OPENSSL_COPY_FILES "${EXTRA_OPENSSL_LIBDIR}/libcrypto-1_1.dll")
    elseif(TARGET_BITS EQUAL 64)
      set(OPENSSL_COPY_FILES "${EXTRA_OPENSSL_LIBDIR}/libcrypto-1_1-x64.dll")
	endif()
  endif()
else()
  set(CMAKE_MODULE_PATH ${ORIGINAL_CMAKE_MODULE_PATH})
  find_package(OpenSSL)
  set(CMAKE_MODULE_PATH ${OWN_CMAKE_MODULE_PATH})
  
  if(OPENSSL_FOUND)
    set(OPENSSL_CRYPTO_LIBRARIES ${OPENSSL_CRYPTO_LIBRARY})
    set(OPENSSL_INCLUDE_DIRS ${OPENSSL_INCLUDE_DIR})
  endif()
endif()